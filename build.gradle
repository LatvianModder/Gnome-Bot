plugins {
	id 'java'
	id 'application'
	id 'com.github.johnrengelman.shadow' version "7.1.2"
}

group 'dev.gnomebot'
version '1.0.0'
archivesBaseName = 'gnome-bot'
mainClassName = 'dev.gnomebot.app.App'

sourceCompatibility = JavaVersion.VERSION_21
targetCompatibility = JavaVersion.VERSION_21

compileJava.options.encoding = 'UTF-8'

tasks.withType(JavaCompile) {
	options.encoding = 'UTF-8'
}

allprojects {
	configurations.all {
		resolutionStrategy.cacheChangingModulesFor 60, 'seconds'
	}
}

repositories {
	mavenCentral()
	//maven { url 'https://jitpack.io' }

	maven {
		url "https://www.cursemaven.com"
		content {
			includeGroup "curse.maven"
		}
	}

	maven {
		url "https://maven.latvian.dev/releases"
		content {
			includeGroup "dev.latvian.apps"
			includeGroup "dev.latvian.mods"
		}
	}

	maven {
		url 'https://oss.sonatype.org/content/repositories/snapshots'
	}
}

dependencies {
	implementation('dev.latvian.apps:tiny-java-server:1.0.0-build.25')
	implementation('dev.latvian.apps:ansi:1.0.0-build.9')
	implementation('dev.latvian.apps:json:1.0.0-build.4')
	implementation('dev.latvian.apps:webutils:1.1.0-build.66')
	implementation('dev.latvian.mods:rhino:2101.2.5-build.54') { transitive = false }
	implementation('com.google.code.gson:gson:2.10.1') // Required by Rhino
	// implementation('net.dv8tion:JDA:5.2.1')
	implementation('com.discord4j:discord4j-core:3.3.0-SNAPSHOT') { changing = true }
	implementation('org.mongodb:mongodb-driver-sync:5.0.0')
	implementation('org.slf4j:slf4j-simple:2.0.7')
	implementation('com.google.guava:guava:31.1-jre') { exclude group: "com.google.code.findbugs" }
	implementation('com.liferay:org.apache.commons.fileupload:1.2.2.LIFERAY-PATCHED-1')
	compileOnly('org.jetbrains:annotations:24.0.1')
}

jar {
	manifest {
		attributes 'Implementation-Version': archiveVersion, 'Main-Class': mainClassName
	}
}

processResources {
	inputs.property "version", project.version

	filesMatching("build.json") {
		expand "version": project.version
	}
}

tasks.shadowJar.dependsOn tasks.processResources